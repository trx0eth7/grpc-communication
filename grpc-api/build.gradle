plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'com.google.protobuf' version "${protobufPluginVersion}"
}

group 'ru.trx.grpc'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

publishing {
    publications {
        myLibrary(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    compileOnly "jakarta.annotation:jakarta.annotation-api:${jakartaAnnotationVersion}"

    // test
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/proto'
        }
        java {
            srcDir 'src/main/java'
            srcDir 'src/generated-sources/main/grpc'
            srcDir 'src/generated-sources/main/java'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }

    generatedFilesBaseDir = "$projectDir/src/generated-sources"

    clean {
        delete generatedFilesBaseDir
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

idea {
    module {
        sourceDirs += file("src/generated-sources/main/java")
        sourceDirs += file("src/generated-sources/main/grpc")
        generatedSourceDirs += file("src/generated-sources/main/java")
        generatedSourceDirs += file("src/generated-sources/main/grpc")
    }
}

tasks.withType(Copy).all {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}
